# TACTYK recursion demo
#
# This uses the microcontext stack to store intermediate values from a modified random walk algorithm
#   The random walk has an energy limit, and it avoids visiting position previously reached.
#   All open positions are placed on the stack.
#   When out of energy, trapped, or out of bounds, the stack is unwound and the walk proceeds from a
#   preceding state.
#   When there are no more open positions, it returns, waits for the user to close the window, then exits.


struct global
  8 width
  8 height
  8 padding

struct sdl_state
  8 mpos_x
  8 mpos_y
  8 key
  1 closing
  1 mousemoved
  6 unused
  8 padding

mem global_vars global 1

extmem video
mem s_state sdl_state 1

text header
  TACTYK Example - Recursion
  This uses a randomized recursive backtracker to draw a blob.  This does not use memory very efficiently...
  Press 'q' or 'escape' to quit.  Or close the window.  It all suppsedly works.

text footer
  Done.

const newline 10
const FRAME_WIDTH 640
const FRAME_HEIGHT 480

MAIN:
  bind addr1 header
  bind addr2 global_vars
  bind addr3 s_state
  bind addr4 video
  assign b 0
  
  PRINT_HEADER_CHAR:
  load byte a addr1 b
  ccall printchar
  add b 1
  if b < header_size PRINT_HEADER_CHAR
  
  assign a newline
  ccall printchar

  # obtain an SDL-backed framebuffer
  tcall sdl_init
  assign a 4
  assign b 3
  assign c FRAME_WIDTH
  assign d FRAME_HEIGHT
  tcall fb_new
  store qword addr2 global.width a
  
  tcall fb_render
  
  # iterate each visible pixel in the frame buffer

  assign b FRAME_WIDTH
  assign c FRAME_HEIGHT
  div b 2
  div c 2
  assign e 255
  
  lwcall RANDDRAW

  FINISH:
  tcall fb_update
  tcall fb_render

  WAIT:
  assign a 50
  ccall sleep
  
  lwcall CHK_UI_STATE
  
  tcall fb_render
  
  goto WAIT
  
  CLOSE_AND_EXIT:  
  # close the SDL window and clean up
  tcall fb_release
  tcall sdl_quit
  
  bind addr1 footer
  assign b 0
  
  PRINT_FOOTER_CHAR:
  load byte a addr1 b
  ccall printchar
  add b 1
  if b < footer_size PRINT_FOOTER_CHAR

  assign a newline
  ccall printchar
  
  ccall printint
  exit

  RANDDRAW:
  tcall fb_update
  tcall fb_render
  assign a 1
  ccall sleep
  
  lwcall CHK_UI_STATE
  
  mctxpush
  stash sB b sC c sE e
  ccall a rand
  and a 3
  if a == 0 NORTH
  if a == 1 EAST
  if a == 2 SOUTH
  goto WEST
  
  WEST:
  sub b 1
  goto PUTIT

  NORTH:
  add c 1
  goto PUTIT

  EAST:
  add b 1
  goto PUTIT

  SOUTH:
  sub c 1
  goto PUTIT
  
  PUTIT:
  if b < 0 SKIPIT
  if c < 0 SKIPIT
  if b >= FRAME_WIDTH SKIPIT
  if c >= FRAME_HEIGHT SKIPIT
  
  load qword f addr2 global.width
  assign d c
  mul d f
  add d b
  mul d 4
  
  load byte a addr4 d
  if a != 0 SKIPIT

  store byte addr4 d e
  add d 1
  store byte addr4 d e
  add d 1
  store byte addr4 d e

  sub e 1
  if e <= 0 SKIPIT

  lwcall RANDDRAW

  stash b sB c sC e sE
  lwcall RANDDRAW

  stash b sB c sC e sE
  lwcall RANDDRAW

  stash b sB c sC e sE
  lwcall RANDDRAW

  stash b sB c sC e sE
  lwcall RANDDRAW
  
  SKIPIT:
  mctxpop
  lwreturn

CHK_UI_STATE:
  tcall sdl_update_state
  load qword a addr3 sdl_state.key
  if a == 20 CLOSE_AND_EXIT
  if a == 41 CLOSE_AND_EXIT
  load byte a addr3 sdl_state.closing
  if a == 1 CLOSE_AND_EXIT
  lwreturn

