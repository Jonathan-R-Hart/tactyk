# -----------------------------------------------------------------------------------------------------------------------------------------------------------------------------
#   Copyright 2023, Jonathan Hart
#   This program is free software: you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation,
#   either version 3 of the License, or (at your option) any later version.
#   This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR
#   PURPOSE. See the GNU General Public License for more details.
#   You should have received a copy of the GNU General Public License along with this program. If not, see <https://www.gnu.org/licenses/>.
# -----------------------------------------------------------------------------------------------------------------------------------------------------------------------------

# Basic framebuffer rendering example using an auxilliary SDL interface

text header
  TACTYK Example - Framebuffer rendering with SDL

text footer
  Done.

const newline 10
const width 640
const height 480

MAIN:
  bind addr1 header
  assign b 0
  
  PRINT_HEADER_CHAR:
  load byte a addr1 b
  tcall print-text-a
  add b 1
  if b < header_size PRINT_HEADER_CHAR
  
  tcall print-newline
  
  # obtain an SDL-backed framebuffer
  assign a width
  assign b height
  tcall sdl--new
  
  tcall sdl--get-eventview
  bind addr3 addr1
  tcall sdl--get-framebuffer
  bind addr4 addr1
  
  # result
  # a = buffer width
  # b = buffer height

  assign b a
  tcall sdl--render
  
  # iterate each visible pixel in the frame buffer
  assign f 0
  sub f 1
  ROW:
    add f 1
    if f == height UPDATE_FB
    assign e 0
    sub e 1
    COL:
      add e 1
      if e == width ROW
      
      # obtain the framebuffer offset of the 1st byte of the pixel (red channel)
      assign d f
      mul d b
      add d e
      mul d 4

      # horizontal red gradient
      assign a e
      mul a 256
      div a width
      store byte addr4 d a

      # offset of 2nd byte (green channel)
      add d 1

      # vertical green gradient
      assign a f
      mul a 256
      div a height
      store byte addr4 d a
  
      goto COL
  UPDATE_FB:
  tcall sdl--upload-framebuffer
  tcall sdl--render
  
  # show it for 1.5 seconds, then exit.
  tcall sleep 1500
  
  # close the SDL window and clean up
  tcall sdl--release
  tcall sdl--quit
  
  bind addr1 footer
  assign b 0
  
  PRINT_FOOTER_CHAR:
  load byte a addr1 b
  tcall print-text-a
  add b 1
  if b < footer_size PRINT_FOOTER_CHAR
  tcall print-newline

  exit



